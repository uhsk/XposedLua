Properties sProperties = null

/**
 * 开发：配置文件
 */
ext.getLocalProperties = {
    if (sProperties != null) {
        return sProperties
    }
    sProperties = new Properties()
    File localPropertiesFile = new File("local.properties")
    if (localPropertiesFile.exists()) {
        sProperties.load(new FileInputStream(localPropertiesFile))
    }
    return sProperties
}

/**
 * 开发：设备ID
 */
ext.getLocalPropertiesDeviceUuid = {
    Properties properties = getLocalProperties()
    return properties.getProperty("develop.uuid", "")
}

/**
 * 获取配置信息，优先系统配置
 */
ext.getPropertyFirstSystem = { system, local, default_ ->
    if (System.getProperty(system, null) != null) {
        return System.getProperty(system, default_)
    }
    Properties properties = getLocalProperties()
    return properties.getProperty(local, default_)
}

/**
 * 构建：开启混淆
 */
ext.getPropertiesBuildProguard = {
    return Boolean.parseBoolean(getPropertyFirstSystem("BUILD_PROGUARD", "build.proguard", "true"))
}

/**
 * 构建：使用三方混淆
 */
ext.getPropertiesBuildAllatori = {
    return Boolean.parseBoolean(getPropertyFirstSystem("BUILD_ALLATORI", "build.allatori", "true"))
}

/**
 * 构建：使用三方混淆
 */
ext.getPropertiesBuildResGuard = {
    return Boolean.parseBoolean(getPropertyFirstSystem("BUILD_RESGUARD", "build.resguard", "true"))
}

/**
 * 构建：正式版密码
 */
ext.getPropertiesBuildReleasePassword = {
    return getPropertyFirstSystem("BUILD_RELEASE_PASSWORD", "build.release_password", "")
}
